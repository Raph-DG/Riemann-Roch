--import Mathlib
import RiemannRoch.OrderOfVanishing.OrdLemmas
--import Plausible

variable {R : Type*} [CommRing R]

open Ring

/--
Variation of `ord_mul` where the user has to show the first input is a non
zero divisor rather than the second.
-/
lemma ord_mul' (R : Type u_1) [CommRing R] {a b : R} (ha : a ‚àà nonZeroDivisors R) :
    ord R (a * b) = ord R a + ord R b := by
  rw [mul_comm, ord_mul R ha, add_comm]

/--
For `x : R` a non zero divisor, `ord R (x^n) = n ‚Ä¢ ord R x`.
-/
@[simp]
theorem ord_pow (x : R) (hx : x ‚àà nonZeroDivisors R) (n : ‚Ñï) : ord R (x ^ n) = n ‚Ä¢ ord R x := by
  induction n with
  | zero => simp
  | succ n ih =>
    rw [pow_succ, ord_mul, ih, succ_nsmul]
    exact hx

/--
For `x : R` a non zero divisor, `ord R (-x) = ord R x`.
-/
@[simp]
lemma ord_neg (x : R) : ord R (-x) = ord R x:= by
  simp only [ord]
  congr 2
  all_goals exact Ideal.span_singleton_neg x

/--
In an `S` algebra `R`, the order of vanishing of `x : R` is equal to the order of vanishing
of `a ‚Ä¢ x` for `a` a unit in `S`.
-/
@[simp]
lemma ord_smul {S : Type*} [CommRing S] [Algebra S R]
    (a : S) (h : IsUnit a) (x : R) : ord R (a ‚Ä¢ x) = ord R x := by
  simp only [ord]
  have : a ‚Ä¢ x = algebraMap S R a * x := by
    exact Algebra.smul_def a x
  rw [this]
  have : IsUnit (algebraMap S R a) := by
    exact RingHom.isUnit_map (algebraMap S R) h
  congr 2
  all_goals exact Ideal.span_singleton_mul_left_unit this x


/--
In an `S` algebra `R`, the order of vanishing of `x : R` is less than or equal
to the order of vanishing of `a ‚Ä¢ x` for any `a : S`. One should note that the order here
is the order on `‚Ñï‚àû` where `‚àû` is a top element.
-/
lemma ord_le_smul {S : Type*} [CommRing S] [Algebra S R] (a : S) (x : R) :
    ord R x ‚â§ ord R (a ‚Ä¢ x) := by
  simp only [ord]
  suffices Ideal.span {a ‚Ä¢ x} ‚â§ Ideal.span {x} by
    let g : (R ‚ß∏ Ideal.span {a ‚Ä¢ x}) ‚Üí‚Çó[R] (R ‚ß∏ Ideal.span {x}) := Submodule.factor this
    refine Module.length_le_of_surjective (Submodule.factor this) (Submodule.factor_surjective this)
  rw [@Ideal.span_singleton_le_span_singleton]
  rw [@Algebra.smul_def']
  exact Dvd.intro_left (Algebra.algebraMap a) rfl

variable [IsDomain R]

/--
The order of vanishing of a unit is `0`.
-/
@[simp]
lemma ord_of_isUnit (x : R) (hx : IsUnit x) : ord R x = 0 := by
  obtain ‚ü®x, rfl‚ü© := hx
  have : ord R ‚Üë(x‚Åª¬π) + ord R x = 0 := by
    rw[‚Üê ord_mul, Units.inv_mul, ord_one]
    simp
  exact eq_zero_of_add_left this

section IsDiscreteValuationRing

variable [IsDiscreteValuationRing R]
/--
In a discrete valuation ring, the order of vanishing of an irreducible element is `1`.
-/
theorem ord_irreducible (œñ : R) (hœñ : Irreducible œñ) : ord R œñ = 1 := by
  rw[Ring.ord, Module.length_eq_one_iff]
  have : (Ideal.span {œñ}).IsMaximal :=
    PrincipalIdealRing.isMaximal_of_irreducible hœñ
  constructor

  intro I
  rw[or_iff_not_imp_right]
  intro hI

  let J : Submodule R R := I.comap (Algebra.ofId R (R ‚ß∏ Ideal.span {œñ})).toLinearMap
  have hJ1 : J ‚â† ‚ä§ := by
    contrapose! hI with hJ
    rw[eq_top_iff] at hJ ‚ä¢
    intro x hx
    obtain ‚ü®x, rfl‚ü© := Ideal.Quotient.mk_surjective x
    specialize hJ (trivial : x ‚àà ‚ä§)
    simpa [J] using hJ

  have hJ2 : Ideal.span {œñ} ‚â§ J := by
    intro x hx
    have : (Ideal.Quotient.mk (Ideal.span {œñ})) x = 0 := by
      rwa [@Ideal.Quotient.eq_zero_iff_mem]
    simp only [J, Submodule.mem_comap, AlgHom.toLinearMap_apply]
    convert I.zero_mem

  have aux := this.eq_of_le (J := J) hJ1 hJ2
  rw[eq_bot_iff]
  intro x hx
  obtain ‚ü®x, rfl‚ü© := Ideal.Quotient.mk_surjective x
  suffices x ‚àà J by
    rw [‚Üê aux] at this
    simpa only [Submodule.mem_bot, @Ideal.Quotient.eq_zero_iff_mem]
  simpa [J] using hx
#min_imports
/--
In a discrete valuation ring `R`, if the order of vansihing of `x` and `y` is
the same then `x` and `y` must be associated.
-/
lemma associated_of_ord_eq (x y : R) (hx : x ‚â† 0) (hy : y ‚â† 0)
    (h : ord R x = ord R y) : Associated x y := by
  obtain ‚ü®œñ, hœñ‚ü© := IsDiscreteValuationRing.exists_irreducible R
  obtain ‚ü®m, Œ±, rfl‚ü© := IsDiscreteValuationRing.eq_unit_mul_pow_irreducible hx hœñ
  obtain ‚ü®n, Œ≤, rfl‚ü© := IsDiscreteValuationRing.eq_unit_mul_pow_irreducible hy hœñ
  nth_rewrite 2 [mul_comm] at h
  rw [mul_comm, ord_mul, ord_mul, ord_pow, ord_pow] at h
  ¬∑ simp only [ord_irreducible œñ hœñ, nsmul_eq_mul, mul_one, Units.isUnit, ord_of_isUnit, add_zero,
    Nat.cast_inj] at h
    simp [h, Associated.refl]
  all_goals aesop

/--
For `x y : R` where `R` is a discrete vakuation ring, we have that
`min (ord R x) (ord R y) ‚â§ ord R (x + y)`. It should be noted that the order
we're using here is the order on `‚Ñï‚àû`, where `‚ä§` is greater than everhing else.
This is relevant since when we're working with `ordFrac` we work with `‚Ñ§·µê‚Å∞`, where the
order instance has the `0` element less than everything else.
-/
theorem ord_add (x y : R) : min (Ring.ord R x) (Ring.ord R y) ‚â§ Ring.ord R (x + y) := by
  classical
  obtain ‚ü®œñ, hœñ‚ü© := IsDiscreteValuationRing.exists_irreducible R
  by_cases hx0 : x = 0
  ¬∑ simp[hx0]
  by_cases hy0 : y = 0
  ¬∑ simp[hy0]
  obtain ‚ü®m, Œ±, hx‚ü© := IsDiscreteValuationRing.eq_unit_mul_pow_irreducible hx0 hœñ
  obtain ‚ü®n, Œ≤, hy‚ü© := IsDiscreteValuationRing.eq_unit_mul_pow_irreducible hy0 hœñ
  wlog hmn : m ‚â§ n
  ¬∑ rw[min_comm, add_comm]
    apply this y x œñ hœñ hy0 hx0 n Œ≤ hy m Œ± hx
    omega
  obtain ‚ü®k, rfl‚ü© := Nat.exists_eq_add_of_le hmn
  have : x + y = (Œ± + Œ≤*œñ^k) * œñ^m := by
    rw[hx, hy]
    ring
  rw[this, hx, hy, ord_mul, ord_mul, ord_mul, ord_pow, ord_pow,
     ord_of_isUnit (Œ± : R), ord_of_isUnit (Œ≤ : R), ord_irreducible _ hœñ]
  ¬∑ simp
  all_goals simp[hœñ.ne_zero]

end IsDiscreteValuationRing

section ordFrac

variable [IsNoetherianRing R] [KrullDimLE 1 R]
variable {K : Type*} [Field K] [Algebra R K] [IsFractionRing R K]

/--
For any nonzero `x : R`, `ordFrac R (algebraMap R K x) ‚â• 1`. Note here that this last
expression is in `‚Ñ§·µê‚Å∞`, so the syntax may be slightly different than expected. Namely,
the `1` here is `WithZero.exp 0`, and so would usually be written as `0` in the additive
context. Further, the order here is different to similar lemmas involving `Ring.ord`, since
here the order is on `‚Ñ§·µê‚Å∞` has the `‚àû` element less than everything else, whereas in `Ring.ord`
we work with the order on `‚Ñï‚àû` where the `‚àû` element is interpreted as a `‚ä§` element.
-/
lemma ordFrac_ge_one_of_ne_zero (x : R) (hx : x ‚â† 0) :
    ordFrac R (algebraMap R K x) ‚â• 1 := by
  simp [ordFrac_eq_ord R x hx, ordMonoidWithZeroHom_eq_ord x (by simp [hx])]
  suffices ord R x ‚â† ‚ä§ by
    rw [ENat.ne_top_iff_exists] at this
    obtain ‚ü®m, hm‚ü© := this
    rw[‚Üê hm]
    have := WithZero.map'_coe (AddMonoidHom.toMultiplicative (Nat.castAddMonoidHom ‚Ñ§)) m
    have : AddMonoidHom.toMultiplicative (Nat.castAddMonoidHom ‚Ñ§) m ‚â• 1 := by
      simp [‚Üê ofAdd_zero, Multiplicative.ofAdd_le]
    suffices WithZero.map' (AddMonoidHom.toMultiplicative (Nat.castAddMonoidHom ‚Ñ§)) 0 ‚â§
            (WithZero.map' (AddMonoidHom.toMultiplicative (Nat.castAddMonoidHom ‚Ñ§))) (m : ‚Ñï‚àû) by
      exact left_eq_inf.mp rfl
    apply WithZero.map'_mono
    ¬∑ rw [AddMonoidHom.coe_toMultiplicative]
      apply Monotone.comp
      ¬∑ exact fun ‚¶Éa b‚¶Ñ a ‚Ü¶ a
      ¬∑ apply Monotone.comp
        ¬∑ simp_all [Nat.mono_cast]
        ¬∑ exact fun ‚¶Éa b‚¶Ñ a ‚Ü¶ a
    ¬∑ aesop
  simp [Ring.ord]
  have := isFiniteLength_quotient_span_singleton R (x := x) (by simp[hx])
  exact Module.length_ne_top_iff.mpr this

/--
For `R` an `S` algebra (with a corresponding compatible action on `K`, the field of fractions
of `R`), for `f : K` and `a : S`, we have that `ordFrac R f ‚â§ ordFrac R (a ‚Ä¢ f)` as long as
`algebraMap S R a ‚â† 0`.
-/
lemma ordFrac_le_smul {S : Type*} [CommRing S] [Algebra S R] [Algebra S K]
    [l : IsScalarTower S R K] (a : S) (ha : algebraMap S R a ‚â† 0) (f : K) :
    Ring.ordFrac R f ‚â§ Ring.ordFrac R (a ‚Ä¢ f) := by
  by_cases j : f = 0
  ¬∑ simp[j]
  suffices (ordFrac R) f ‚â§ (ordFrac R) ((algebraMap S K) a ‚Ä¢ f) by simp_all only [ne_eq,
    algebraMap_smul]
  simp only [smul_eq_mul, map_mul]
  suffices (ordFrac R) ((algebraMap S K) a) ‚â• 1 by exact le_mul_of_one_le_left' this
  suffices (ordFrac R) ((algebraMap R K) (algebraMap S R a)) ‚â• 1 by
    simpa [l.algebraMap_eq]
  apply ordFrac_ge_one_of_ne_zero
  exact ha

end ordFrac

/-
This is in another file

lemma ord_cast_mono : Monotone <|
    (WithZero.map' (AddMonoidHom.toMultiplicative (Nat.castAddMonoidHom ‚Ñ§))) := by
  intro a b h
  apply WithZero.map'_mono ?_ h
  intro x y hy
  simp_all-/


section experiment

/-
This section is an attempt to make some lemmas which make working with
WithZero (Multiplicative A) a little bit less painful.

(The kind of pain I'm referring to can be seen in the current proof of
ordFrac_add, which does a lot of rewriting manually).
-/

lemma WithZero.map_eq_top_map {Œ± : Type*} {Œ≤ : Type*} [MulOneClass Œ±] [MulOneClass Œ≤] (f : Œ± ‚Üí* Œ≤) :
    (WithZero.map' f).toFun = WithTop.map f.toFun := by
  simp only [map', lift', Equiv.coe_fn_mk, MonoidHom.coe_comp, Function.comp_apply,
    coeMonoidHom_apply, ZeroHom.toFun_eq_coe, ZeroHom.coe_mk, WithTop.map, OneHom.toFun_eq_coe,
    MonoidHom.toOneHom_coe]
  ext x : 1
  split
  next x =>
    simp_all only [Option.map_none]
    rfl
  next x a =>
    simp_all only [Option.map_some]
    rfl

lemma ord_cast_mono_top : @Monotone _ _ (by infer_instance : Preorder (WithTop (Multiplicative ‚Ñï)))
    (by infer_instance : Preorder (WithTop (Multiplicative ‚Ñ§))) <|
    (WithZero.map' (AddMonoidHom.toMultiplicative (Nat.castAddMonoidHom ‚Ñ§))) := by
  intro a b h

  suffices @LE.le _ (by infer_instance : LE (WithTop (Multiplicative ‚Ñ§)))
           ((WithZero.map' (AddMonoidHom.toMultiplicative (Nat.castAddMonoidHom ‚Ñ§))).toFun a)
           ((WithZero.map' (AddMonoidHom.toMultiplicative (Nat.castAddMonoidHom ‚Ñ§))).toFun b) from
    this
  suffices (WithTop.map (AddMonoidHom.toMultiplicative (Nat.castAddMonoidHom ‚Ñ§))) a ‚â§
           (WithTop.map (AddMonoidHom.toMultiplicative (Nat.castAddMonoidHom ‚Ñ§))) b by
    rwa [WithZero.map_eq_top_map]
  apply Monotone.withTop_map ?_ h
  intro x y h
  simp_all

lemma Multiplicative.withZero_le_iff_withTop_le_of_ne_zero {Œ± : Type*} [Preorder Œ±]
   (a b : WithZero (Multiplicative Œ±)) (ha : a ‚â† 0) (hb : b ‚â† 0) :
    a ‚â§ b ‚Üî @LE.le _ (by infer_instance : LE (WithTop (Multiplicative Œ±))) a b := by
  rw [WithZero.ne_zero_iff_exists] at ha hb
  obtain ‚ü®a', ha'‚ü© := ha
  obtain ‚ü®b', hb'‚ü© := hb
  rw[‚Üê ha', ‚Üê hb', WithZero.coe_le_coe]
  have (a : (Multiplicative Œ±)) : WithZero.coe a = WithTop.some a := rfl
  rw[this, this, WithTop.coe_le_coe]


lemma WithZero.multiplicative_ne_zero_iff_exists {Œ± : Type*} {x : WithZero (Multiplicative Œ±)} :
    x ‚â† 0 ‚Üî ‚àÉ a : Œ±, WithZero.coe (Multiplicative.ofAdd a) = x := by
  constructor
  ¬∑ intro h
    obtain ‚ü®a, ha‚ü© := WithZero.ne_zero_iff_exists.mp h
    use Multiplicative.toAdd a
    exact ha
  ¬∑ rintro ‚ü®a, ha‚ü©
    rw [‚Üê ha]
    exact coe_ne_zero

lemma WithZero.multiplicative_coe_div {Œ± : Type u_1} [Sub Œ±] (a b : Œ±) :
    WithZero.coe (Multiplicative.ofAdd (a - b)) =
    WithZero.coe (Multiplicative.ofAdd a) / WithZero.coe (Multiplicative.ofAdd b) := rfl

lemma WithZero.multiplicative_coe_add {Œ± : Type u_1} [Add Œ±] (a b : Œ±) :
    WithZero.coe (Multiplicative.ofAdd (a + b)) =
    WithZero.coe (Multiplicative.ofAdd a) * WithZero.coe (Multiplicative.ofAdd b) := rfl

end experiment

set_option maxHeartbeats 0
/--
For `x y : R`, if `x + y ‚â† 0` then `min (ordFrac R x) (ordFrac R y) ‚â§ ordFrac R (x + y)`. The
condition that `x + y ‚â† 0` is used to guarantee that all the elements we're taking `ordFrac` of
are nonzero, meaning none of them will be `0` in `‚Ñ§·µê‚Å∞`. This allows us to use `ord_add` (which
uses the ordering on `‚Ñï‚àû`), since these orders correspond on non `‚ä§` elements.
-/
theorem ordFrac_add [IsNoetherianRing R] [KrullDimLE 1 R] [IsDiscreteValuationRing R]
    {K : Type*} [Field K] [Algebra R K] [IsFractionRing R K] (x y : K)
    (h : x + y ‚â† 0) :
    min (Ring.ordFrac R x) (Ring.ordFrac R y) ‚â§ Ring.ordFrac R (x + y) := by

  by_cases hx : x = 0
  ¬∑ simp [hx]
  by_cases hy : y = 0
  ¬∑ simp [hy]

  let x1 := (IsLocalization.sec (nonZeroDivisors R) x).1
  let x2 := (IsLocalization.sec (nonZeroDivisors R) x).2
  let y1 := (IsLocalization.sec (nonZeroDivisors R) y).1
  let y2 := (IsLocalization.sec (nonZeroDivisors R) y).2

  have x1nez : x1 ‚â† 0 := IsLocalization.sec_fst_ne_zero hx
  have y1nez : y1 ‚â† 0 := IsLocalization.sec_fst_ne_zero hy

  have hx1 : x1 ‚àà nonZeroDivisors R := mem_nonZeroDivisors_of_ne_zero x1nez
  have hy1 : y1 ‚àà nonZeroDivisors R := mem_nonZeroDivisors_of_ne_zero y1nez

  have : Ring.ordFrac R (x + y) =
      ordFrac R (IsLocalization.mk' K (x1 * y2 + x2 * y1) (x2*y2)) := by
    have : x1 * ‚Üëy2 + ‚Üëx2 * y1 = x1 * ‚Üëy2 + y1 * ‚Üëx2 := by simp [CommMonoid.mul_comm]

    rw [this, IsLocalization.mk'_add (S := K) x1 y1 x2 y2,
       IsLocalization.mk'_sec, IsLocalization.mk'_sec]
  rw [this]

  have : Ring.ordFrac R x = ordFrac R (IsLocalization.mk' K x1 x2) := by
    congr
    exact Eq.symm (IsLocalization.mk'_sec K x)
  rw [this]
  have : Ring.ordFrac R y = ordFrac R (IsLocalization.mk' K y1 y2) := by
    congr
    exact Eq.symm (IsLocalization.mk'_sec K y)
  rw [this]

  have o : x1 * y2 + x2 * y1 ‚â† 0 := by
    simp only [ne_eq, x1, y2, x2, y1]
    have : (IsLocalization.mk' K x1 x2) + (IsLocalization.mk' K y1 y2) ‚â† 0 := by
      rw[‚Üê IsLocalization.mk'_sec (M := nonZeroDivisors R) K x,
         ‚Üê IsLocalization.mk'_sec (M := nonZeroDivisors R) K y] at h
      simpa using h
    simp at this
    field_simp [x1, x2, y1, y2] at this

    rw[‚Üê map_mul, ‚Üê map_mul, ‚Üê map_add] at this
    exact fun h ‚Ü¶ (this (by aesop)).elim


  have : ordFrac R (IsLocalization.mk' K (x1 * y2 + x2 * y1) (x2*y2)) =
      ordMonoidWithZeroHom R (x1 * y2 + x2 * y1) / ordMonoidWithZeroHom R (x2*y2) :=
    ordFrac_eq_div R ‚ü®(x1 * y2 + x2 * y1), mem_nonZeroDivisors_of_ne_zero o‚ü© (x2*y2)

  rw[this]
  have ans := ord_add (x1*y2) (x2*y1)
  rw [inf_le_iff] at ans ‚ä¢
  rw [ordFrac_eq_div R ‚ü®x1, hx1‚ü© x2, ordFrac_eq_div R ‚ü®y1, hy1‚ü© y2]
  rw[ord_le_iff, ord_le_iff] at ans
  ¬∑ simp only [map_mul] at ans ‚ä¢
    have : (ordMonoidWithZeroHom R) x1 ‚â† 0 := ordMonoidWithZeroHom_ne_zero x1 hx1
    obtain ‚ü®x1', hx1'‚ü© := WithZero.ne_zero_iff_exists.mp this
    rw[‚Üê hx1'] at ans ‚ä¢
    have : (ordMonoidWithZeroHom R) x2 ‚â† 0 := ordMonoidWithZeroHom_ne_zero x2.1 x2.2
    obtain ‚ü®x2', hx2'‚ü© := WithZero.ne_zero_iff_exists.mp this
    rw[‚Üê hx2'] at ans ‚ä¢
    have : (ordMonoidWithZeroHom R) (x1 * ‚Üëy2 + ‚Üëx2 * y1) ‚â† 0 := ordMonoidWithZeroHom_ne_zero
        (x1 * ‚Üëy2 + ‚Üëx2 * y1) (mem_nonZeroDivisors_of_ne_zero o)
    obtain ‚ü®sum, hsum‚ü© := WithZero.ne_zero_iff_exists.mp this
    rw[‚Üê hsum] at ans ‚ä¢
    have  : (ordMonoidWithZeroHom R) y1 ‚â† 0 := ordMonoidWithZeroHom_ne_zero y1 hy1
    obtain ‚ü®y1', hy1'‚ü© := WithZero.ne_zero_iff_exists.mp this
    rw[‚Üê hy1'] at ans ‚ä¢
    have : (ordMonoidWithZeroHom R) y2 ‚â† 0 := ordMonoidWithZeroHom_ne_zero y2.1 y2.2
    obtain ‚ü®y2', hy2'‚ü© := WithZero.ne_zero_iff_exists.mp this

    rw [‚Üê hy2'] at ans ‚ä¢
    rw [‚Üê WithZero.coe_div, ‚Üê WithZero.coe_div, ‚Üê WithZero.coe_mul]
    rw [‚Üê WithZero.coe_mul, ‚Üê WithZero.coe_mul, WithZero.coe_le_coe, WithZero.coe_le_coe] at ans
    rw [‚Üê WithZero.coe_div, WithZero.coe_le_coe, WithZero.coe_le_coe]
    obtain h | h := ans
    ¬∑ left
      suffices x1'.toAdd - x2'.toAdd ‚â§ sum.toAdd - (x2'.toAdd + y2'.toAdd) by exact this
      have h : x1'.toAdd + y2'.toAdd ‚â§ sum.toAdd := h
      omega
    ¬∑ right
      suffices y1'.toAdd - y2'.toAdd ‚â§ sum.toAdd - (x2'.toAdd + y2'.toAdd) by exact this
      have h : x2'.toAdd + y1'.toAdd ‚â§ sum.toAdd := h
      omega

  ¬∑ exact mul_mem_nonZeroDivisors.mpr ‚ü®x2.2, hy1‚ü©
  ¬∑ exact mem_nonZeroDivisors_of_ne_zero o
  ¬∑ exact mul_mem_nonZeroDivisors.mpr ‚ü®hx1, y2.2‚ü©
  ¬∑ exact mem_nonZeroDivisors_of_ne_zero o

/--
In a discrete valuation ring `R` with fraction ring `K`, if `x y : K` and
`ordFrac R x = ordFrac R y`, then `x` must only differ from `y` by a unit of `R`.
-/
theorem associated_of_ordFrac_eq [IsNoetherianRing R] [KrullDimLE 1 R] [IsDiscreteValuationRing R]
    {K : Type*} [Field K] [Algebra R K] [IsFractionRing R K] (x y : K) (hx : x ‚â† 0) (hy : y ‚â† 0)
    (h : ordFrac R x = ordFrac R y) : ‚àÉ u : RÀ£, u ‚Ä¢ x = y := by
  let x1 := (IsLocalization.sec (nonZeroDivisors R) x).1
  let x2 := (IsLocalization.sec (nonZeroDivisors R) x).2
  let y1 := (IsLocalization.sec (nonZeroDivisors R) y).1
  let y2 := (IsLocalization.sec (nonZeroDivisors R) y).2

  have x1nez : x1 ‚â† 0 := IsLocalization.sec_fst_ne_zero hx
  have y1nez : y1 ‚â† 0 := IsLocalization.sec_fst_ne_zero hy

  rw[‚Üê IsLocalization.mk'_sec (M := nonZeroDivisors R) K x,
     ‚Üê IsLocalization.mk'_sec (M := nonZeroDivisors R) K y] at h ‚ä¢
  rw[ordFrac_eq_div R ‚ü®x1, by simp [x1nez]‚ü© x2] at h
  rw[ordFrac_eq_div R ‚ü®y1, by simp [y1nez]‚ü© y2] at h
  simp [x1nez, y1nez] at h

  obtain ‚ü®œñ, hœñ‚ü© := IsDiscreteValuationRing.exists_irreducible R
  obtain ‚ü®nx1, Œ±x1, hx1‚ü© := IsDiscreteValuationRing.eq_unit_mul_pow_irreducible x1nez hœñ
  obtain ‚ü®nx2, Œ±x2, hx2‚ü© :=
      IsDiscreteValuationRing.eq_unit_mul_pow_irreducible (by aesop : x2.1 ‚â† 0) hœñ
  obtain ‚ü®ny1, Œ±y1, hy1‚ü© := IsDiscreteValuationRing.eq_unit_mul_pow_irreducible y1nez hœñ
  obtain ‚ü®ny2, Œ±y2, hy2‚ü© :=
      IsDiscreteValuationRing.eq_unit_mul_pow_irreducible (by aesop : y2.1 ‚â† 0) hœñ

  suffices ‚àÉ u : RÀ£, u ‚Ä¢ IsLocalization.mk' K x1 x2 = IsLocalization.mk' K y1 y2 from this
  have hx2' : Œ±x2 * œñ ^ nx2 ‚àà nonZeroDivisors R := by aesop
  have hy2' : Œ±y2 * œñ ^ ny2 ‚àà nonZeroDivisors R := by aesop

  have hx2'' : x2 = ‚ü®Œ±x2 * œñ ^ nx2, hx2'‚ü© := by
    ext
    rw[hx2]

  have hy2'' : y2 = ‚ü®Œ±y2 * œñ ^ ny2, hy2'‚ü© := by
    ext
    rw[hy2]
  rw[hx1, hy1, hx2'', hy2'']
  rw [hx1, hx2, hy1, hy2, ord_mul', ord_mul', ord_mul', ord_mul',
      ord_pow, ord_pow, ord_pow, ord_pow] at h
  ¬∑ simp only [Units.isUnit, ord_of_isUnit, ord_irreducible œñ hœñ, nsmul_eq_mul, mul_one,
    zero_add] at h
    let u := Œ±x1‚Åª¬π * Œ±y2‚Åª¬π * Œ±y1 * Œ±x2
    use u
    simp [Units.smul_def, Algebra.smul_def]
    field_simp
    rw[div_eq_div_iff]
    ¬∑ suffices algebraMap R K (u * Œ±x1 * œñ ^ nx1 * Œ±y2 * œñ ^ ny2) =
               algebraMap R K (Œ±y1 * (œñ ^ ny1) * Œ±x2 * (œñ ^ nx2)) by
        repeat rw [‚Üê map_pow]
        repeat rw [‚Üê map_mul]
        ring_nf at this ‚ä¢
        exact this

      suffices ‚Üëu * ‚ÜëŒ±x1 * œñ ^ nx1 * ‚ÜëŒ±y2 * œñ ^ ny2 =
               Œ±y1 * (œñ ^ ny1) * Œ±x2 * (œñ ^ nx2) by
        rw [this]
      have : œñ ^ nx1 * œñ ^ ny2 = œñ ^ ny1 * œñ ^ nx2 := by
        have : nx1 + ny2 = ny1 + nx2 := by
          rw[div_eq_div_iff] at h
          ¬∑ repeat rw[‚Üê map_mul] at h
            have := Nat.cast_withZero_mul_int_injective h
            have : (nx1 : ‚Ñï‚àû) + ny2 = ny1 + nx2 := this
            repeat rw [‚Üê ENat.coe_add] at this
            rwa [ENat.coe_inj] at this
          all_goals exact Ne.symm (not_eq_of_beq_eq_false rfl)
        repeat rw [‚Üê pow_add]
        rw [this]
      simp[u]
      field_simp
      grind
    all_goals aesop
  all_goals aesop


--#check HSMul
open AlgebraicGeometry
variable (X : Scheme) [IsIntegral X] (z : X) (U : X.Opens) (h : z ‚àà U)
noncomputable local instance : Algebra ‚ÜëŒì(X, U) ‚Üë(X.presheaf.stalk z) := (X.presheaf.germ U z h).hom.toAlgebra in
theorem test (X : Scheme) [IsIntegral X] (z : X) (U : X.Opens) (h : z ‚àà U) (f g : Œì(X, U))
    [Nonempty U] (u : Œì(X, U))
    (hfg :
      let alg : Algebra ‚ÜëŒì(X, U) ‚Üë(X.presheaf.stalk z) := (X.presheaf.germ U z h).hom.toAlgebra
      @algebraMap Œì(X, U) (X.presheaf.stalk z) _ _ (X.presheaf.germ U z h).hom.toAlgebra f = (u ‚Ä¢ (@algebraMap Œì(X, U) (X.presheaf.stalk z) _ _ (X.presheaf.germ U z h).hom.toAlgebra g) : X.presheaf.stalk z))
    : f = u * g := by
  let alg : Algebra ‚ÜëŒì(X, U) ‚Üë(X.presheaf.stalk z) :=
        (X.presheaf.germ U z h).hom.toAlgebra
  have : u ‚Ä¢ algebraMap (Œì(X, U)) (X.presheaf.stalk z) g =
    algebraMap (Œì(X, U)) (X.presheaf.stalk z) u *
    algebraMap (Œì(X, U)) (X.presheaf.stalk z) g := rfl

  simp_rw [this] at hfg

  have : algebraMap (Œì(X, U)) (X.presheaf.stalk z) u *
    algebraMap (Œì(X, U)) (X.presheaf.stalk z) g =
    algebraMap (Œì(X, U)) (X.presheaf.stalk z) (u * g) := by exact Eq.symm (algebraMap.coe_mul u g)
  rw[this] at hfg
  have : Function.Injective (algebraMap ‚ÜëŒì(X, U) ‚Üë(X.presheaf.stalk z)) :=
    AlgebraicGeometry.germ_injective_of_isIntegral X z h
  exact this hfg
/-
Let X be a scheme and z a point of X. If f and g are elements of `Œì (X, U)`
for `U` some open s.t. `z ‚àà U`, and we have that:
`algebraMap (Œì (X, U)) (X.presheaf.stalk z) f = u ‚Ä¢ algebraMap (Œì (X, U)) (X.presheaf.stalk z) g`
for some `u` a unit in `Œì (X, U)`, then we should have that `f = u ‚Ä¢ g`

Well, `u ‚Ä¢ algebraMap (Œì (X, U)) (X.presheaf.stalk z) g =`
      `algebraMap (Œì (X, U)) (X.presheaf.stalk z) u *`
      `algebraMap (Œì (X, U)) (X.presheaf.stalk z) g`
      `= algebraMap (Œì (X, U)) (X.presheaf.stalk z) (u * g)`

And since algebraMap ought to be injective, I think this means we get `f = u*g`.
So perhaps we win without much further playing.

If that's the case, we then have a notion of tensor product which is good as.
We then need to implement the fundamental exact sequence,
0 ‚Üí ùí™‚Çì(-D) -> ùí™‚Çì -> ùí™‚Çê -> 0

-/

#min_imports
